comment doc {
    A simple model for influenze
}

variables {
    global Susceptible Infected Recovered
    Susceptible.output = 1
    Infected.output = 1
    Recovered.output = 1
}



condition INF {
    transmission_mode = proximity
    transmissibility = 1.0
    exposed_state = E
    meta_start_state = Import

    state S {
        Susceptible = Susceptible + 1
        INF.sus = 1
        wait()
        next()
    }

    state E {
        Susceptible = Susceptible - 1
        INF.sus = 0
        print_file(infections.csv, id, today, age, sex)
        wait(24*lognormal(1.9,1.23))
        next(Ia) with prob(0.33)
        default(Is)
    }

    state Is {
        Infected = Infected + 1
        INF.trans = 1
        wait(24* lognormal(5.0,1.5))
        next(R)
    }

    state Ia {
        Infected = Infected + 1
        INF.trans = 0.5
        wait( 24 * lognormal(5.0, 1.5) )
        next(R)
    }

    state R {
        Infected = Infected - 1
        Recovered = Recovered + 1
        INF.trans = 0
        wait()
        next()
    }

    state Import {
        open_file(infections.csv, id, date, age, sex)
        import_count(10)
        wait()
        next()
    }
}
