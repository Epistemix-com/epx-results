comment {
    Provides agents with a `my_age_bracket_index` personal variable. This
    enables them to update global list variables that collect counts of agents
    entering states stratified by age.

    Age brackets are 0-10, 10-20, ..., 70+. Each agent is assigned a
    `my_age_bracket_index` variable in the `AGE_BRACKET.Check` state with which
    they select the correct element to update of other lists that collect
    age-stratified simulation run data.
}


variables {
    global_list g_list_of_age_bracket_upper_bounds
    g_list_of_age_bracket_upper_bounds = list(10, 20, 30, 40, 50, 60, 70, 200)

    global g_num_age_brackets

    personal my_age_bracket_index
}


condition AGE_BRACKET {
    # Assign `my_age_bracket_index` for each agent

    meta_start_state = MetaStart

    state MetaStart {
        g_num_age_brackets = length(g_list_of_age_bracket_upper_bounds)
        wait()
        default()
    }

    state Start {
        wait(0)
        next(WaitForAdmin)
    }

    state WaitForAdmin {
        # Ensures the meta agent has time to set g_num_age_brackets
        wait(1)
        next(Check)
    }

    state Check {
        if (age / 10 <= (g_num_age_brackets - 1)) \
            then my_age_bracket_index = floor(age / 10)
        if (age / 10 > (g_num_age_brackets - 1)) \
            then my_age_bracket_index = g_num_age_brackets - 1
        wait(0)
        default(Done)
    }

    state Done {
        wait()
        default()
    }

}
